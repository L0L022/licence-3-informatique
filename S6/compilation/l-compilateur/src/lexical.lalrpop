grammar;

use crate::lexer::{Token, LexicalError};
use crate::lexical::*;

use crate::lexer::Token as T;

pub Program = <Statement*>;

Statement = {
    DclFunction,
    DclVariable,
};

DclFunction : Statement = <i:Id> "(" <p:Comma<Scalar>> ")" <v:(<Comma<Variable>> ";")?> "{" <is:Instruction*> "}" => Statement::DclFunction(i, p, v.unwrap_or(Vec::new()), is);
DclVariable : Statement = <Comma<Variable>> ";" => Statement::DclVariable(<>);

Variable : Variable = {
    Scalar => Variable::Scalar(<>),
    Vector => Variable::Vector(<>),
};

Scalar : Scalar = <t:Id> "$" <i:Id> => (Type::try_from(t).unwrap(), i);
Vector : Vector = <t:Id> "$" <i:Id> "[" <o:Integer> "]" => (Type::try_from(t).unwrap(), o, i);

Instruction : Instruction = {
    <Affectation> ";",
    <Expression> ";" => Instruction::Eval(<>),
    Return <(Expression)?> ";" => Instruction::Return(<>),
    If <e:BooleanExpression> Then "{" <i1:Instruction*> "}" <i2:(Else "{" <Instruction*> "}")?> => Instruction::If(e, i1, i2.unwrap_or(Vec::new())),
    While <BooleanExpression> Do "{" <Instruction*> "}" => Instruction::While(<>),
};

Affectation : Instruction = <LeftValue> "=" <Expression> => Instruction::Affectation(<>);

Expression = ExpressionL1;

ExpressionL1 = {
    ExpressionL2,
    <l:ExpressionL1> <o:OperationExpressionL1> <r:ExpressionL2> => Expression::Operation(o, Box::new(l), Box::new(r)),
};

OperationExpressionL1 : ArithmeticOperator = {
    "+" => ArithmeticOperator::Addidion,
    "-" => ArithmeticOperator::Subtraction,
};

ExpressionL2 = {
    ExpressionL3,
    <l:ExpressionL2> <o:OperationExpressionL2> <r:ExpressionL3> => Expression::Operation(o, Box::new(l), Box::new(r)),
};

OperationExpressionL2 : ArithmeticOperator = {
    "*" => ArithmeticOperator::Multiplication,
    "/" => ArithmeticOperator::Division,
};

ExpressionL3 = {
    AtomicExpression,
    "(" <ExpressionL1> ")",
};

AtomicExpression : Expression = {
    Integer => Expression::Value(<>),
    "-" <Integer> => Expression::Value(-<>),
    CallFunction,
    LeftValue => Expression::LeftValue(<>),
};

LeftValue : LeftValue = "$" <i:Id> <o:("[" <Expression> "]")?> => match o {
        Some(o) => LeftValue::VariableAt(i, Box::new(o)),
        None => LeftValue::Variable(i),
};

CallFunction : Expression = <i:Id> "(" <e:Comma<Expression>> ")" => Expression::CallFunction(i, e);

BooleanExpression = BooleanExpressionL1;

BooleanExpressionL1 = {
    BooleanExpressionL2,
    <l:BooleanExpressionL1> "|" <r:BooleanExpressionL2> => BooleanExpression::Or(Box::new(l), Box::new(r)),
};

BooleanExpressionL2 = {
    BooleanExpressionL3,
    <l:BooleanExpressionL2> "&" <r:BooleanExpressionL3> => BooleanExpression::And(Box::new(l), Box::new(r)),
};

BooleanExpressionL3 = {
    AtomicBooleanExpression,
    "(" <BooleanExpressionL1> ")",
    "!" <BooleanExpressionL3> => BooleanExpression::Not(Box::new(<>)),
};

AtomicBooleanExpression : BooleanExpression = {
    <Expression> "=" <Expression> => BooleanExpression::Equal(<>),
    <Expression> "<" <Expression> => BooleanExpression::LessThan(<>),
};

Comma<T>: ::std::vec::Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token {
        Integer => T::Integer(<i32>),
        Id => T::Id(<String>),

        "," => T::Comma,
        ";" => T::Semicolon,
        "$" => T::Dollar,

        // Instructions

        Return => T::Return,
        If => T::If,
        Then => T::Then,
        Else => T::Else,
        While => T::While,
        Do => T::Do,

        // Brackets

        "(" => T::OpenParenthesis,
        ")" => T::CloseParenthesis,
        "{" => T::OpenCurlyBracket,
        "}" => T::CloseCurlyBracket,
        "[" => T::OpenSquareBracket,
        "]" => T::CloseSquareBracket,

        // Operators

        "+" => T::Addition,
        "-" => T::Subtraction,
        "*" => T::Multiplication,
        "/" => T::Division,
        "<" => T::LessThan,
        "=" => T::Equal,
        "&" => T::And,
        "|" => T::Or,
        "!" => T::Not,
    }
}
